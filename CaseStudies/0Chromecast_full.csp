#import "Lib_HomeScan";
enum{ChromecastWiFiBeacon, ChromecastSSID, ChromecastBSSID, OpenSystemAuthenticationRequest, AssociationResponse, GetEurekaInfo, EurekaInfo,
 NewNonce, Name,SetupState,EthernetConnected,IPaddress,SsdpUdn,ModelName,DeviceCapabilities,SSIDSuffix,TosAccepted,BSSID,SDPublicKey,Certificate,
 IntermediateCerts,SignedData,PostScanWifi, SuccessScanResponse, GetScanResults,
 HomeWifiSSID, HomeWifiBSSID, Frequency, SignalLevel, WPAAuth7, WPACipher4, PostConnectWifi, Password, EncryptedPassword, Test, SuccessConnectRequest,
 NewIPAddress,Version,NewSetupState, PostSetEurekaInfo,NewName,OptInStatusTrue, SuccessSetRequest, PostSaveWifi, ImmediateTrue, SuccessSaveRequest,
 MDNSDiscoveryRequest, MDNSDiscoveryResponse, ValidScreenID, GetMdxSessionStatus, ScreenID, GetLoungeToken, Expiration,
 ScreenIDAssociation, GetScreenAvailability, AvailabilityResponse, PostBindRequest, Device,ID,ControlPointName,
 AppName, MethodSetPlayList, VideoID, CurrentVideoID, SID, GsessionID, LoungeStatus, PlaylistModified,
 OnAutoplayModeChanged, OnPlaylistModeChanged, SignRParam,VersionRParam,NameRParam,SetupStateRParam,EthernetConnectedRParam,
 IPaddressRParam,SsdpUdnRParam,ModelNameRParam,DeviceCapabilitiesRParam,SSIDSuffixRParam,TosAcceptedRParam,PublicKeyRParam,BSSIDRParam,
 Aenc, Adec, SDPrivateKey};

// Wifi channels
channel wifi0 0;
channel openwifi 0;
channel wifi 0;

#define NONCE_LOWER_BOUND_INCLUSIVE 0;
#define NONCE_UPPER_BOUND_INCLUSIVE 2;


/// MAIN ENTRY POINT TO THE WHOLE SYSTEM
#define NUMBER_OF_CPS 3; // if this is changed - change SDBroadcastWiFiBeacon to send more or less beacons out
ASystem =AutoGeneratedAttacker() || CPPar(NUMBER_OF_CPS) || SD() || GS();// ||| Attacker();

// For testing attacker properties
AttackedSystem() = CPPar(NUMBER_OF_CPS - 1) ||| SD() ||| GS() ||| Attacker();
AttackedWifiSystem() = CPPar(NUMBER_OF_CPS) ||| SD() ||| GS() ||| ScreenIDAttacker();
AttackedScreenIDSystem() = CPPar(NUMBER_OF_CPS) ||| SD() ||| GS() ||| ScreenIDControlAttacker();

/**************

Break between Initialization and Smart Device (SD)

**************/

var sdReceivedNonce = -1;
var sdReceivedPassword = -1;
// Interaction sequence for the SD (the Chromecast)
SD() = SDBroadcastWiFiBeacon();
// BR = {1} in Transaction 1
// Sends 3 beacons to correspond with NUMBER_OF_CPS
SDBroadcastWiFiBeacon() = atomic{
	wifi0!ChromecastWiFiBeacon.ChromecastSSID.ChromecastBSSID 
	->  wifi0!ChromecastWiFiBeacon.ChromecastSSID.ChromecastBSSID 
	->  wifi0!ChromecastWiFiBeacon.ChromecastSSID.ChromecastBSSID -> Skip()};
	(SDReceiveAuthRequest() [] SDBroadcastWiFiBeacon());
SDReceiveAuthRequest() = wifi0?ChromecastSSID.OpenSystemAuthenticationRequest -> SDSendAssociationResponse();
SDSendAssociationResponse() = wifi0!AssociationResponse -> SDReceiveEurekaInfoRequest();

// Start of Authentication segment

SDReceiveEurekaInfoRequest() =
	openwifi?GetEurekaInfo.SignRParam.VersionRParam.NameRParam.SetupStateRParam.EthernetConnectedRParam.IPaddressRParam.SsdpUdnRParam.ModelNameRParam.DeviceCapabilitiesRParam.SSIDSuffixRParam.TosAcceptedRParam.PublicKeyRParam.BSSIDRParam.newNonce 
	 -> saveNonce{sdReceivedNonce = newNonce} 
	 -> SDSendEurekaInfo();
// Branch possibility from 5 -> 4
SDSendEurekaInfo() = 
	openwifi!EurekaInfo.Version.Name.SetupState.EthernetConnected.IPaddress.SsdpUdn.ModelName.DeviceCapabilities.SSIDSuffix.TosAccepted.BSSID.SDPublicKey.Certificate.IntermediateCerts.SignedData.sdReceivedNonce
	-> (SDReceivePostScanWifi() [] SDReceiveEurekaInfoRequest());
SDReceivePostScanWifi() = openwifi?PostScanWifi -> SDSendSuccessScanResponse();
// Branch possibility from 7 -> 6
SDSendSuccessScanResponse() = openwifi!SuccessScanResponse -> (SDReceivePostScanWifi() [] SDReceiveGetScanResults());
SDReceiveGetScanResults() = openwifi?GetScanResults -> SDSendScanResults();
// Branch possibility from 9 -> 8
SDSendScanResults() = openwifi!HomeWifiSSID.HomeWifiBSSID.Frequency.SignalLevel.WPAAuth7.WPACipher4 -> (SDReceiveGetScanResults() [] SDReceivePostConnectWifi());
SDReceivePostConnectWifi() = openwifi?PostConnectWifi.HomeWifiSSID.Aenc.password.PublicKey.WPAAuth7.WPACipher4 
	-> savePassword{sdReceivedPassword = password}
	-> SDSendSuccessConnectRequest();
SDSendSuccessConnectRequest() = 
	adec.sdReceivedPassword.SDPublicKey.SDPrivateKey 
	-> openwifi!SuccessConnectRequest.Adec.sdReceivedPassword.SDPublicKey.SDPrivateKey 
	-> SDReceiveGetEurekaInfo2();
SDReceiveGetEurekaInfo2() = openwifi?GetEurekaInfo.IPaddressRParam.VersionRParam.SetupStateRParam -> SDSendEurekaInfo2();
SDSendEurekaInfo2() = openwifi!NewIPAddress.Version.NewSetupState -> SDReceivePostSetEurekaInfo();
SDReceivePostSetEurekaInfo() = openwifi?PostSetEurekaInfo.NewName.OptInStatusTrue -> SDSendSuccessSetRequest();
// Branch possibility from 15 -> 14
SDSendSuccessSetRequest() = openwifi!SuccessSetRequest -> (SDReceivePostSetEurekaInfo() [] SDReceivePostSaveWifi());
SDReceivePostSaveWifi() = openwifi?PostSaveWifi.ImmediateTrue -> SDSendSuccessSaveRequest();
SDSendSuccessSaveRequest() = openwifi!SuccessSaveRequest -> SDReceiveMDNSDiscoveryRequest();
SDReceiveMDNSDiscoveryRequest() = wifi?MDNSDiscoveryRequest -> (SDReceiveMDNSDiscoveryRequest() [] SDSendMDNSDiscoveryResponse());
SDSendMDNSDiscoveryResponse() = wifi!MDNSDiscoveryResponse -> SDSendValidScreenID();
SDSendValidScreenID() = wifi!ValidScreenID -> SDReceiveGetMdxSessionStatus();
SDReceiveGetMdxSessionStatus() = wifi?GetMdxSessionStatus -> SDSendScreenID();
SDSendScreenID() = wifi!ScreenID -> Skip();

/**************

Break between Smart Device (SD) and Control Point (CP)

**************/

// Limit the nonce range to prevent infinite state space
var cpNonce : {NONCE_LOWER_BOUND_INCLUSIVE..NONCE_UPPER_BOUND_INCLUSIVE} = NONCE_LOWER_BOUND_INCLUSIVE;
var cpSavedSDPublicKey = -1;
var encryptedPassword = -1;
// Interaction sequence for the CP (control point aka user-facing controller of the Chromecast)
CPPar(numcps) = ||| {numcps} @ CP();
CP() = CPReceiveWiFiBeacon();
// Include guard condition for accepting the wifi beacon as legitimate from the SD
CPReceiveWiFiBeacon() = wifi0?ChromecastWiFiBeacon.ChromecastSSID.ChromecastBSSID 
		-> CPSendAuthRequest();
CPSendAuthRequest() = wifi0!ChromecastSSID.OpenSystemAuthenticationRequest 
		-> CPReceiveAssociationResponse();

// Start of Authentication segment
CPReceiveAssociationResponse() = wifi0?assocResponse 
		-> CPSendGetEurekaInfo;

CPSendGetEurekaInfo() = generateNewCPNonce{cpNonce = (cpNonce + 1) % (NONCE_UPPER_BOUND_INCLUSIVE + 1)} ->
	openwifi!GetEurekaInfo.SignRParam.VersionRParam.NameRParam.SetupStateRParam.EthernetConnectedRParam.IPaddressRParam.SsdpUdnRParam.ModelNameRParam.DeviceCapabilitiesRParam.SSIDSuffixRParam.TosAcceptedRParam.PublicKeyRParam.BSSIDRParam.cpNonce 
	-> CPReceiveCerts();
// This is the 5th -> 4th branch - very possible that we will re-send this message
CPReceiveCerts() = openwifi?[nonce == cpNonce && certificate == Certificate]EurekaInfo.Version.Name.SetupState.EthernetConnected.IPaddress.SsdpUdn.ModelName.DeviceCapabilities.SSIDSuffix.TosAccepted.BSSID.publicKey.certificate.IntermediateCerts.SignedData.nonce
	 -> verify{cpSavedSDPublicKey = publicKey}
	 -> (CPSendGetEurekaInfo() [] CPSendPostScanWifi());
CPSendPostScanWifi() = openwifi!PostScanWifi -> CPReceiveSuccessScanResponse();
// Branch possibility from 7 -> 6	
CPReceiveSuccessScanResponse() = openwifi?SuccessScanResponse -> (CPSendGetScanResults() [] CPSendPostScanWifi());
CPSendGetScanResults() = openwifi!GetScanResults -> CPReceiveScanResults();
// Branch possibility from 9 -> 8
CPReceiveScanResults() = openwifi?homeWifiSSID.homeWifiBSSID.frequency.signalLevel.wpaAuth7.wpaCipher4 -> 
(CPSendGetScanResults() [] CPSendPostConnectWifi(homeWifiSSID, wpaAuth7, wpaCipher4));
CPSendPostConnectWifi(homeWifiSSID, wpaAuth7, wpaCipher4) = 
	aenc.Password.cpSavedSDPublicKey ->
	openwifi!PostConnectWifi.homeWifiSSID.Aenc.Password.cpSavedSDPublicKey.wpaAuth7.wpaCipher4 -> CPReceiveSuccessConnectRequest(); 
CPReceiveSuccessConnectRequest() = successConn -> openwifi?SuccessConnectRequest.Adec.Password.SDPublicKey.SDPrivateKey  
	-> CPSendGetEurekaInfo2();
CPSendGetEurekaInfo2() = openwifi!GetEurekaInfo.IPaddressRParam.VersionRParam.SetupStateRParam  -> CPReceiveEurekaInfo2();
CPReceiveEurekaInfo2() = openwifi?NewIPAddress.Version.NewSetupState -> CPSendPostSetEurekaInfo();	
CPSendPostSetEurekaInfo() = openwifi!PostSetEurekaInfo.NewName.OptInStatusTrue -> CPReceiveSuccessSetRequest();
// Branch possibility from 15 -> 1411
CPReceiveSuccessSetRequest() = openwifi?SuccessSetRequest -> (CPSendPostSetEurekaInfo() [] CPSendPostSaveWifi());
CPSendPostSaveWifi() = openwifi!PostSaveWifi.ImmediateTrue -> CPReceiveSuccessSaveRequest();
CPReceiveSuccessSaveRequest() = openwifi?SuccessSaveRequest -> CPSendMDNSDiscoveryRequest();
CPSendMDNSDiscoveryRequest() = wifi!MDNSDiscoveryRequest -> (CPSendMDNSDiscoveryRequest() [] CPReceiveMDNSDiscoveryResponse());
CPReceiveMDNSDiscoveryResponse() = wifi?MDNSDiscoveryResponse -> CPSendGetMdxSessionStatus();
CPSendGetMdxSessionStatus() = wifi!GetMdxSessionStatus -> CPReceiveScreenID();
CPReceiveScreenID() = wifi?ScreenID -> CPSendGetLoungeToken();
CPSendGetLoungeToken() = wifi!GetLoungeToken.ScreenID -> CPReceiveAssociation();
// Branch possibility from 24 -> 25/27 
CPReceiveAssociation() = wifi?ScreenIDAssociation.Expiration.ScreenID -> (CPSendGetScreenAvailability() [] CPSendPostBindRequest()); 

// Start of Control Segment
CPSendGetScreenAvailability() = wifi!GetScreenAvailability.ScreenIDAssociation -> CPReceiveAvailabilityResponse();
CPReceiveAvailabilityResponse() = wifi?AvailabilityResponse.ScreenIDAssociation -> (CPSendGetScreenAvailability() [] CPSendPostBindRequest());
CPSendPostBindRequest() = wifi!PostBindRequest.ScreenIDAssociation.Device.ID.ControlPointName.AppName.MethodSetPlayList.VideoID -> CPReceiveCurrentVideoID();
// Branch possibility from 28 -> 27/29 
CPReceiveCurrentVideoID() = wifi?CurrentVideoID.SID.GsessionID.LoungeStatus.PlaylistModified.OnAutoplayModeChanged.OnPlaylistModeChanged -> 
	(CPSendPostBindRequest() [] CPSendPostBindRequestWithoutVideoID());
CPSendPostBindRequestWithoutVideoID() = wifi!PostBindRequest.ScreenIDAssociation.Device.ID.ControlPointName.AppName.MethodSetPlayList -> CPReceiveCurrentVideoIDWithoutStreaming();
CPReceiveCurrentVideoIDWithoutStreaming() = wifi?CurrentVideoID.SID.GsessionID.LoungeStatus.PlaylistModified.OnAutoplayModeChanged.OnPlaylistModeChanged -> 
	(CPSendPostBindRequestWithoutVideoID() [] Skip());
	
	
/**************

Break between Smart Device (SD) and Google Servers (GS)

**************/

GS() = GSReceiveValidScreenID();
GSReceiveValidScreenID() = wifi?ValidScreenID -> GSReceiveGetLoungeToken();
GSReceiveGetLoungeToken() = wifi?GetLoungeToken.screenID -> GSSendAssociation(); 
// Branch possibility from 24 -> 25/27 
GSSendAssociation() = wifi!ScreenIDAssociation.Expiration.ScreenID 
	-> saveMapScreenIDAndAssociation
	-> (GSReceiveGetScreenAvailability() [] GSReceivePostBindRequest());

// Start of Control segment
GSReceiveGetScreenAvailability() = wifi?GetScreenAvailability.ScreenIDAssociation -> verifyAssociation1 -> GSSendAvailabilityResponse();
// Branch possibility from 26 -> 25
GSSendAvailabilityResponse() = wifi!AvailabilityResponse.ScreenIDAssociation -> (GSReceiveGetScreenAvailability() [] GSReceivePostBindRequest());
GSReceivePostBindRequest() = wifi?PostBindRequest.ScreenIDAssociation.Device.ID.ControlPointName.AppName.MethodSetPlayList.VideoID -> verifyAssociation2 -> GSSendCurrentVideoID();
// Branch possibility from 28 -> 27/29 
GSSendCurrentVideoID() = gsStreamVideo -> wifi!CurrentVideoID.SID.GsessionID.LoungeStatus.PlaylistModified.OnAutoplayModeChanged.OnPlaylistModeChanged -> 
	(GSReceivePostBindRequest() [] GSReceivePostBindRequestWithoutVideoID());
GSReceivePostBindRequestWithoutVideoID() = wifi?PostBindRequest.ScreenIDAssociation.Device.ID.ControlPointName.AppName.MethodSetPlayList -> verifyAssociation -> GSSendCurrentVideoIDWithoutStreaming();
GSSendCurrentVideoIDWithoutStreaming() = wifi!CurrentVideoID.SID.GsessionID.LoungeStatus.PlaylistModified.OnAutoplayModeChanged.OnPlaylistModeChanged -> 
	(GSReceivePostBindRequestWithoutVideoID() [] Skip());

/**************

Break between Google Servers (GS) and attackers
	
**************/

var assocReceived = false;
var gotSDParams = false;
var attackerSavedSDPublicKey = -1;
var isWifiSaved = false;

// The  attacker only needs to create an OpenSystemAuthenticationRequest to begin interacting with the SD
// Verification 1
Attacker() = wifi0?ccBeacon.ccSSID.ccBSSID -> AttackerSendAuthRequest(ccSSID);
AttackerSendAuthRequest(ccSSID) = wifi0!ccSSID.OpenSystemAuthenticationRequest -> AttackerReceiveAssociationResponse();
AttackerReceiveAssociationResponse() = wifi0?assocResponse -> assocResponseReceived{assocReceived = true} -> AttackerSendGetEurekaInfo();

//  Verification 2
var attackerNonce : {NONCE_LOWER_BOUND_INCLUSIVE..NONCE_UPPER_BOUND_INCLUSIVE} = NONCE_LOWER_BOUND_INCLUSIVE;
AttackerSendGetEurekaInfo() = generateNeAttackerCPNonce{attackerNonce = (attackerNonce + 1) % (NONCE_UPPER_BOUND_INCLUSIVE + 1)} ->
	openwifi!GetEurekaInfo.SignRParam.VersionRParam.NameRParam.SetupStateRParam.EthernetConnectedRParam.IPaddressRParam.SsdpUdnRParam.ModelNameRParam.DeviceCapabilitiesRParam.SSIDSuffixRParam.TosAcceptedRParam.PublicKeyRParam.BSSIDRParam.attackerNonce 
	-> AttackerReceiveCerts();
// This is the 5th -> 4th branch - very possible that we will re-send this message
AttackerReceiveCerts() = openwifi?EurekaInfo.Version.Name.SetupState.EthernetConnected.IPaddress.SsdpUdn.ModelName.DeviceCapabilities.SSIDSuffix.TosAccepted.BSSID.publicKey.certificate.IntermediateCerts.SignedData.nonce
	 -> gotSDParameters{gotSDParams = true}
	 -> savePublicKeyAttacker{attackerSavedSDPublicKey = publicKey}
	 -> (AttackerSendGetEurekaInfo() [] AttackerSendPostScanWifi());
	 
// Verification that we can connect the SD to our own WiFi
// Assume that our wifi network is in range and homewifiSSID (our malicious SSID) is returned.
AttackerSendPostScanWifi() = openwifi!PostScanWifi -> AttackerReceiveSuccessScanResponse();
// Branch possibility from 7 -> 6	
AttackerReceiveSuccessScanResponse() = openwifi?SuccessScanResponse -> (AttackerSendGetScanResults() [] AttackerSendPostScanWifi());
AttackerSendGetScanResults() = openwifi!GetScanResults -> AttackerReceiveScanResults();
// Branch possibility from 9 -> 8
AttackerReceiveScanResults() = openwifi?homeWifiSSID.homeWifiBSSID.frequency.signalLevel.wpaAuth7.wpaCipher4 -> 
(AttackerSendGetScanResults() [] AttackerSendPostConnectWifi(homeWifiSSID, wpaAuth7, wpaCipher4));
AttackerSendPostConnectWifi(homeWifiSSID, wpaAuth7, wpaCipher4) = 
	aenc.Password.attackerSavedSDPublicKey ->
	openwifi!PostConnectWifi.homeWifiSSID.Aenc.Password.attackerSavedSDPublicKey.wpaAuth7.wpaCipher4 -> AttackerReceiveSuccessConnectRequest(); 
AttackerReceiveSuccessConnectRequest() = successConn -> openwifi?SuccessConnectRequest.Adec.Password.SDPublicKey.SDPrivateKey  
	-> AttackerSendGetEurekaInfo2();
AttackerSendGetEurekaInfo2() = openwifi!GetEurekaInfo.IPaddressRParam.VersionRParam.SetupStateRParam  -> AttackerReceiveEurekaInfo2();
AttackerReceiveEurekaInfo2() = openwifi?NewIPAddress.Version.NewSetupState -> AttackerSendPostSetEurekaInfo();	
AttackerSendPostSetEurekaInfo() = openwifi!PostSetEurekaInfo.NewName.OptInStatusTrue -> AttackerReceiveSuccessSetRequest();
// Branch possibility from 15 -> 1411
AttackerReceiveSuccessSetRequest() = openwifi?SuccessSetRequest -> (AttackerSendPostSetEurekaInfo() [] AttackerSendPostSaveWifi());
AttackerSendPostSaveWifi() = openwifi!PostSaveWifi.ImmediateTrue -> AttackerReceiveSuccessSaveRequest();
AttackerReceiveSuccessSaveRequest() = openwifi?SuccessSaveRequest -> wifiSuccessfullySaved{isWifiSaved = true} -> Skip();
	 
	 
// Assume we have access to 'wifi' channel -
var hasScreenID = false;
ScreenIDAttacker() = ScreenIDAttackerSendMDNSDiscoveryRequest();
ScreenIDAttackerSendMDNSDiscoveryRequest() = wifi!MDNSDiscoveryRequest -> (ScreenIDAttackerSendMDNSDiscoveryRequest() [] ScreenIDAttackerReceiveMDNSDiscoveryResponse());
ScreenIDAttackerReceiveMDNSDiscoveryResponse() = wifi?MDNSDiscoveryResponse -> ScreenIDAttackerSendGetMdxSessionStatus();
ScreenIDAttackerSendGetMdxSessionStatus() = wifi!GetMdxSessionStatus -> ScreenIDAttackerReceiveScreenID();
ScreenIDAttackerReceiveScreenID() = wifi?screenID -> gotScreenID{hasScreenID = true} -> Skip(); //ScreenIDAttackerSendGetLoungeToken();

// Assume now we have the screen ID
ScreenIDControlAttacker() = ScreenIDControlAttackerSendGetLoungeToken();
ScreenIDControlAttackerSendGetLoungeToken() = wifi!GetLoungeToken.ScreenID -> ScreenIDControlAttackerReceiveAssociation();
// Branch possibility from 24 -> 25/27 
ScreenIDControlAttackerReceiveAssociation() = wifi?ScreenIDAssociation.Expiration.ScreenID -> (ScreenIDControlAttackerSendGetScreenAvailability() [] ScreenIDControlAttackerSendPostBindRequest()); 

var gsStreamingVideo = false;
var hasGotCurrentVideoID = false;
// Start of Control Segment
ScreenIDControlAttackerSendGetScreenAvailability() = wifi!GetScreenAvailability.ScreenIDAssociation -> ScreenIDControlAttackerReceiveAvailabilityResponse();
ScreenIDControlAttackerReceiveAvailabilityResponse() = wifi?AvailabilityResponse.ScreenIDAssociation -> (ScreenIDControlAttackerSendGetScreenAvailability() [] ScreenIDControlAttackerSendPostBindRequest());
ScreenIDControlAttackerSendPostBindRequest() = wifi!PostBindRequest.ScreenIDAssociation.Device.ID.ControlPointName.AppName.MethodSetPlayList.VideoID -> ScreenIDControlAttackerReceiveCurrentVideoID();
// Branch possibility from 28 -> 27/29 
ScreenIDControlAttackerReceiveCurrentVideoID() = streamingVideo{gsStreamingVideo = true} -> wifi?CurrentVideoID.SID.GsessionID.LoungeStatus.PlaylistModified.OnAutoplayModeChanged.OnPlaylistModeChanged -> 
	(ScreenIDControlAttackerSendPostBindRequest() [] ScreenIDControlAttackerSendPostBindRequestWithoutVideoID());
ScreenIDControlAttackerSendPostBindRequestWithoutVideoID() = wifi!PostBindRequest.ScreenIDAssociation.Device.ID.ControlPointName.AppName.MethodSetPlayList -> ScreenIDControlAttackerReceiveCurrentVideoIDWithoutStreaming();
ScreenIDControlAttackerReceiveCurrentVideoIDWithoutStreaming() = wifi?CurrentVideoID.SID.GsessionID.LoungeStatus.PlaylistModified.OnAutoplayModeChanged.OnPlaylistModeChanged -> 
	gotCurrentVideoId{hasGotCurrentVideoID = true} ->
	(ScreenIDControlAttackerSendPostBindRequestWithoutVideoID() [] Skip());
	

#assert ASystem deadlockfree;
#assert ASystem divergencefree;
#assert ASystem deterministic;
#assert ASystem nonterminating;
#assert ASystem |= []<> gsStreamVideo;
#assert ASystem |= [](verifyAssociation1 -> <> gsStreamVideo);
#assert ASystem |= [](verifyAssociation2 -> <> gsStreamVideo);

#define goalAssoc assocReceived == true;
#assert AttackedSystem() reaches goalAssoc;

#define goalParams gotSDParams == true;
#assert AttackedSystem() reaches goalParams;

#define goalWifiSave isWifiSaved == true;
#assert AttackedSystem() reaches goalWifiSave;

#define goalScreenID hasScreenID == true;
#assert AttackedWifiSystem() reaches goalScreenID;

#define goalStreamingVideo gsStreamingVideo == true;
#assert AttackedScreenIDSystem() reaches goalStreamingVideo;

#define goalGetCurrentID hasGotCurrentVideoID == true;
#assert AttackedScreenIDSystem() reaches goalGetCurrentID;
var<Knowledge> AutoGeneratedAttackerKnowledge;
AutoGeneratedAttacker()= wifi0?msg1{AutoGeneratedAttackerKnowledge.addKnowledge(msg1)}->AutoGeneratedAttacker()
           []wifi0?msg1.msg2->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg1)}->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg2)}->AutoGeneratedAttacker()
           []wifi0?msg1.msg2.msg3->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg1)}->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg2)}->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg3)}->AutoGeneratedAttacker()
           []openwifi?msg1{AutoGeneratedAttackerKnowledge.addKnowledge(msg1)}->AutoGeneratedAttacker()
           []openwifi?msg1.msg2->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg1)}->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg2)}->AutoGeneratedAttacker()
           []openwifi?msg1.msg2.msg3->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg1)}->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg2)}->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg3)}->AutoGeneratedAttacker()
           []wifi?msg1{AutoGeneratedAttackerKnowledge.addKnowledge(msg1)}->AutoGeneratedAttacker()
           []wifi?msg1.msg2->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg1)}->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg2)}->AutoGeneratedAttacker()
           []wifi?msg1.msg2.msg3->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg1)}->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg2)}->addKnowledge{AutoGeneratedAttackerKnowledge.addKnowledge(msg3)}->AutoGeneratedAttacker()
           []if (AutoGeneratedAttackerKnowledge.knows(ChromecastWiFiBeacon)==true){wifi0!ChromecastWiFiBeacon->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( ChromecastSSID)==true){wifi0! ChromecastSSID->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( ChromecastBSSID)==true){wifi0! ChromecastBSSID->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( OpenSystemAuthenticationRequest)==true){wifi0! OpenSystemAuthenticationRequest->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( AssociationResponse)==true){wifi0! AssociationResponse->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( GetEurekaInfo)==true){wifi0! GetEurekaInfo->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( EurekaInfo)==true){wifi0! EurekaInfo->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows(ChromecastWiFiBeacon)==true){openwifi!ChromecastWiFiBeacon->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( ChromecastSSID)==true){openwifi! ChromecastSSID->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( ChromecastBSSID)==true){openwifi! ChromecastBSSID->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( OpenSystemAuthenticationRequest)==true){openwifi! OpenSystemAuthenticationRequest->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( AssociationResponse)==true){openwifi! AssociationResponse->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( GetEurekaInfo)==true){openwifi! GetEurekaInfo->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( EurekaInfo)==true){openwifi! EurekaInfo->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows(ChromecastWiFiBeacon)==true){wifi!ChromecastWiFiBeacon->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( ChromecastSSID)==true){wifi! ChromecastSSID->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( ChromecastBSSID)==true){wifi! ChromecastBSSID->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( OpenSystemAuthenticationRequest)==true){wifi! OpenSystemAuthenticationRequest->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( AssociationResponse)==true){wifi! AssociationResponse->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( GetEurekaInfo)==true){wifi! GetEurekaInfo->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()}
           []if (AutoGeneratedAttackerKnowledge.knows( EurekaInfo)==true){wifi! EurekaInfo->AutoGeneratedAttacker()}else{AutoGeneratedAttacker()};
